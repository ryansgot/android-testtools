// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.keys = [
            'aws': [
                    'accessKeyId': project.hasProperty("awsMavenAccessKey") ? project.property("awsMavenAccessKey") : "${System.env.AWS_ACCES_KEY_ID}",
                    'secretKey': project.hasProperty("awsMavenSecretKey") ? project.property("awsMavenSecretKey") : "${System.env.AWS_SECRET_KEY}",
            ],
            'bintray': [
                    'user': project.hasProperty('bintrayUser') ? project.getProperty('bintrayUser') : '',
                    'apiKey': project.hasProperty('bintrayApiKey') ? project.getProperty('bintrayApiKey') : ''
            ]
    ]
    ext.versions = [
            'plugins': [
                    'android': '3.3.0',
                    'androidMavenGradle': '2.1',
                    'fsGradlePublishing': '0.0.1'
            ],
            'android': [
                    'compileSdk': 28,
                    'targetSdk': 28,
                    'minSdk': 17,
                    'support': [
                            'lib': '28.0.0',
                            'multidex': '1.0.2'
                    ],
                    'arch': [
                            'room': '1.1.1',
                            'components': '1.0.0'
                    ],
                    'test': [
                            'support': [
                                    'lib': '1.0.2',
                                    'espresso': '3.0.2'
                            ]
                    ]
            ],
            'google': [
                    'jsr305': '3.0.2'
            ],
            'kotlin': '1.3.30',
            'junit': '4.12',
    ]
    ext.deps = [
            'plugins': [
                    'android': "com.android.tools.build:gradle:${versions.plugins.android}",
                    'androidMavenGradle': "com.github.dcendents:android-maven-gradle-plugin:${versions.plugins.androidMavenGradle}",
                    'fsGradlePublishing': "com.fsryan.gradle:fsryan-gradle-publishing:${versions.plugins.fsGradlePublishing}",
                    'kotlin': "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}",
            ],
            'kotlin': [
                    'stdlib': "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${versions.kotlin}",
            ],
            'google': [
                    'jsr305': "com.google.code.findbugs:jsr305:${versions.google.jsr305}"
            ],
            'junit': "junit:junit:${versions.junit}",
            'android': [
                    'arch': [
                            'lifecycle': [
                                    'extensions': "android.arch.lifecycle:extensions:${versions.android.arch.components}"
                            ],
                            'testing': [
                                    'core': "android.arch.core:core-testing:${versions.android.arch.components}"
                            ]
                    ],
                    'support': [
                            'multidex': "com.android.support:multidex:${versions.android.support.multidex}",
                            'annotations': "com.android.support:support-annotations:${versions.android.support.lib}",
                            'test':[
                                    'monitor': "com.android.support.test:monitor:${versions.android.test.support.lib}",
                                    'runner': "com.android.support.test:runner:${versions.android.test.support.lib}",
                                    'rules': "com.android.support.test:rules:${versions.android.test.support.lib}",
                                    'espresso': [
                                            'core': "com.android.support.test.espresso:espresso-core:${versions.android.test.support.espresso}",
                                            'contrib': "com.android.support.test.espresso:espresso-contrib:${versions.android.test.support.espresso}"
                                    ]
                            ]
                    ]
            ]
    ]
    repositories {
        google()
        jcenter()
        maven {
            name 'release'
            url 's3://repo.fsryan.com/release'
            credentials(AwsCredentials) {
                accessKey = keys.aws.accessKeyId
                secretKey = keys.aws.secretKey
            }
        }
        maven {
            name 'snapshot'
            url 's3://repo.fsryan.com/snapshot'
            credentials(AwsCredentials) {
                accessKey = keys.aws.accessKeyId
                secretKey = keys.aws.secretKey
            }
        }
    }
    dependencies {
        classpath deps.plugins.android
        classpath deps.plugins.kotlin
        classpath deps.plugins.androidMavenGradle
        classpath deps.plugins.fsGradlePublishing
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

def configureFSPublishing(Project p, String desc) {
    p.fsPublishingConfig {
        developerName "Ryan Scott"
        developerId "fsryan"
        developerEmail "fsryan.developer@gmail.com"
        siteUrl 'https://github.com/ryansgot/android-testtools'
        baseArtifactId p.name
        groupId p.group
        versionName p.version
        releaseRepoUrl 's3://repo.fsryan.com/release'
        snapshotRepoUrl 's3://repo.fsryan.com/snapshot'
        description desc
        awsAccessKeyId keys.aws.accessKeyId
        awsSecretKey keys.aws.secretKey
    }
}